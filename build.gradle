buildscript { repositories { jcenter() } }
plugins {
  id "com.jfrog.bintray" version "1.6"
  id 'nebula.nebula-release' version '4.0.1'
}

apply plugin: 'maven-publish'
apply plugin: 'groovy'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}

configurations {
  ivy
}

dependencies {
  compile 'io.reactivex:rxjava:1.1.2'

  testCompile(
          'junit:junit:4.12',
          'org.codehaus.groovy:groovy-all:2.4.4',
          'org.spockframework:spock-core:1.0-groovy-2.4'
  )
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
  archives sourcesJar, javadocJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  pkg {
    repo = 'rxgroovy'
    userOrg = 'tednaleid'
    name = "${project.name}"
    desc = "${project.description}"
    websiteUrl = 'https://github.com/tednaleid/rxgroovy-ext'
    issueTrackerUrl = 'https://github.com/tednaleid/rxgroovy-ext/issues'
    vcsUrl = 'https://github.com/tednaleid/rxgroovy-ext.git'
    licenses = ['Apache-2.0']
    publications = ['maven']
    version {
      name = "${project.version}"
      desc = "${project.description}"
      released = new Date()
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.naleid.rx'
      artifactId 'rxgroovy-ext'
      from components.java

      artifact sourcesJar
      artifact javadocJar
    }
  }
}

// to release a new version: `./gradlew final` then go to bintray and click the "publish" button
